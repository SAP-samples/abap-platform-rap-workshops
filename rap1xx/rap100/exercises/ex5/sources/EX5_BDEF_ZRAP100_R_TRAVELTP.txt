managed implementation in class ZRAP100_BP_TravelTP_### unique;
strict ( 2 );
with draft;

define behavior for ZRAP100_R_TRAVELTP_### alias Travel
persistent table zrap100_atrav###
draft table ZRAP100_DTRAV###
etag master LastChangedAt
lock master total etag LocalLastChangedAt
authorization master( global )
early numbering
{
  field ( mandatory : create )
   TravelID;

  field ( readonly )
   CreatedAt,
   CreatedBy,
   LocalLastChangedAt,
   LastChangedAt,
   LastChangedBy;

  field ( readonly : update )
  TravelID;

  field ( mandatory  )
  CustomerID,
  BeginDate,
  EndDate;

  create;
  update;
  delete;

  determination setStatusToOpen on modify { create; }

  validation validateCustomer on save { create; field CustomerID; }
  validation validateDates on save { create; field BeginDate, EndDate; }

  draft action Edit;
  draft action Activate;
  draft action Discard;
  draft action Resume;
  draft determine action Prepare
  {
    validation validateCustomer;
    validation validateDates;    }

  mapping for ZRAP100_ATRAV###
  {
    TravelID = TRAVEL_ID;
    AgencyID = AGENCY_ID;
    CustomerID = CUSTOMER_ID;
    BeginDate = BEGIN_DATE;
    EndDate = END_DATE;
    BookingFee = BOOKING_FEE;
    TotalPrice = TOTAL_PRICE;
    CurrencyCode = CURRENCY_CODE;
    Description = DESCRIPTION;
    OverallStatus = OVERALL_STATUS;
    Attachment = ATTACHMENT;
    MimeType = MIME_TYPE;
    FileName = FILE_NAME;    
    CreatedBy = CREATED_BY;
    CreatedAt = CREATED_AT;
    LastChangedBy = LAST_CHANGED_BY;
    LastChangedAt = LAST_CHANGED_AT;
    LocalLastChangedAt = LOCAL_LAST_CHANGED_AT;
  }
}
