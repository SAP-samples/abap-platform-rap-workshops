managed;

define behavior for ZRAP400_I_TRAVEL_#### alias Travel
implementation in class zrap400_bp_i_travel_#### unique
persistent table zrap400_trav####
etag master LastChangedAt
lock master
authorization master ( instance )
early numbering
{
  create;
  update;
  delete;
  association _Booking { create ( features : instance ); }

  field ( readonly ) LastChangedAt, LastChangedBy, CreatedAt, CreatedBy;
  field ( readonly ) TravelID, TotalPrice;
  field ( mandatory ) AgencyID, CustomerID, BeginDate, EndDate, OverallStatus, CurrencyCode;

  // actions
  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;
  internal action reCalcTotalPrice;

  // determinations
  determination setStatusToOpen on modify { create; }
  determination calculateTotalPrice on modify { create; field BookingFee, CurrencyCode; }

  // validations
  validation validateCustomer on save { create; field CustomerID; }
  validation validateAgency on save { create; field AgencyID; }
  validation validateDates on save { create; field BeginDate, EndDate; }
  validation validateStatus on save { create; field OverallStatus; }

  // field mapping
  mapping for zrap400_trav####
  {
    TravelID      = travel_id;
    AgencyID      = agency_id;
    CustomerID    = customer_id;
    BeginDate     = begin_date;
    EndDate       = end_date;
    BookingFee    = booking_fee;
    TotalPrice    = total_price;
    OverallStatus = overall_status;
    CurrencyCode  = currency_code;
    Description   = description;
    CreatedAt     = created_at;
    CreatedBy     = created_by;
    LastChangedAt = last_changed_at;
    LastChangedBy = last_changed_by;
  }
}

define behavior for ZRAP400_I_BOOKING_#### alias Booking
//implementation in class zrap400_bp_i_booking_#### unique
persistent table zrap400_book####
lock dependent by _Travel
authorization dependent by _Travel
early numbering
{
  update;
  delete;

  field ( readonly ) TravelID, BookingID;

  mapping for ZRAP400_BOOK####
  {
    TravelID = TRAVEL_ID;
    BookingID = BOOKING_ID;
    BookingDate = BOOKING_DATE;
    CustomerID = CUSTOMER_ID;
    CarrierID = CARRIER_ID;
    ConnectionID = CONNECTION_ID;
    FlightDate = FLIGHT_DATE;
    FlightPrice = FLIGHT_PRICE;
    CurrencyCode = CURRENCY_CODE;
    BookingStatus = BOOKING_STATUS;
    LastChangedAt = LAST_CHANGED_AT;
  }

  association _Travel;
}